buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            jcenter()
        }

        dependencies {
            // Had to bump this from 3.5.3 otherwise errors occur after gradle
            // needed to be updated to version 7 for JSK 17
            classpath 'com.android.tools.build:gradle:4.2.0'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    buildFeatures {
        // This is needed for fbjni imports to work (see https://github.com/facebookincubator/fbjni/blob/main/docs/android_setup.md)
        prefab true
    }
    compileSdkVersion safeExtGet('FlexibleHttp_compileSdkVersion', 29)
    defaultConfig {
        minSdkVersion safeExtGet('FlexibleHttp_minSdkVersion', 16)
        targetSdkVersion safeExtGet('FlexibleHttp_targetSdkVersion', 29)
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                // Needed to add this otherwise the fbjni header won't be found
                // https://github.com/facebookincubator/fbjni/issues/50#issuecomment-788869153
                arguments '-DANDROID_STL=c++_shared'
            }
        }

    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.18.1"
        }
    }

    ndkVersion "23.1.7779620"

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dependencies {
      implementation 'com.facebook.fbjni:fbjni:0.2.2'
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
        // url("$rootDir/../node_modules/react-native/ReactCommon")
        // url("$rootDir/../node_modules/react-native/React")
        url("$rootDir/../node_modules/react-native/ReactAndroid")
        url("$rootDir/../node_modules/react-native/ReactAndroid/src/main/jni/third-party/folly")
    }
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    // implementation 'com.facebook.fbjni:fbjni:+'
    // TODO: see if this line is needed
    implementation 'com.facebook.fbjni:fbjni:0.2.2'
}
